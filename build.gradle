buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'docker'
apply plugin: 'com.github.johnrengelman.shadow'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

ext {
    scalaVersion = '2.11'
}

group 'com.jvm-bloggers'
version '0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.typesafe.akka:akka-stream-kafka_2.11:0.12'
    compile 'com.restfb:restfb:1.33.0'
    compile 'org.projectlombok:lombok:1.16.10'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'
    compile 'org.antlr:ST4:4.0.8'
    compile 'net.jodah:failsafe:1.0.0'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'com.github.charithe:kafka-junit:3.0.0'
    testCompile 'cglib:cglib-nodep:3.2.4'
    testCompile 'org.objenesis:objenesis:2.4'
}

String getTimestampWithGitHash() {
    String timeStamp = new Date().format('yyyyMMdd-HHmmss')
    String cmd = "git log --pretty=format:%h -n 1"
    def proc = cmd.execute()
    proc.waitFor()
    return timeStamp + '-' + proc.in.text
}

task buildDocker(type: Docker, dependsOn: build) {
    push = true
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    tagVersion = jar.version + '-' + getTimestampWithGitHash()
    doFirst {
        copy {
            from shadowJar
            into stageDir
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.jvm_bloggers.facebook.client.Application'
    }
}

shadowJar {
    configurations = [project.configurations.compile]
    append('reference.conf')
}
